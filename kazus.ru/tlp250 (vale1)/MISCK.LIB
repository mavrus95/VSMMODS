* HCPL-063L SPICE Macromodel 
* Rev. A
* 10/07
* ZFC 
*
* This is the behavioural model for the above-mentioned part number.
* It is valid for functional simulation over the range specified below.
* Supply range: 2.7 to 3.6V
* Temperature: 25C
*
* Macromodels provided by Avago Technologies are not warranted
* as fully representing all of the specifications and operating 
* characteristics of the product. 
*
* Macromodels are useful for evaluating product performance but they
* cannot model exact device performance under all conditions, nor are
* they intended to replace breadboarding for final verification.
*
* Copyright 2007 Avago Technologies Limited. All Rights Reserved
********************************************************************************
*                pin1    to    pin8
*                 |             |
.subckt HCPL-063L 1 2 3 4 5 6 7 8 
xicc 5 8  icc_063L
xlimitl21 5 24 25 limitl_063L
xlimitl22 5 27 28 limitl_063L
xlimitl12 5 18 19 limitl_063L
xlimitl11 5 15 16 limitl_063L
vinside 13 5 DC 10
xcomp21 5 13 5 23 24 comparator_063L
xcomp22 5 13 5 26 27 comparator_063L
xcomp12 5 13 5 17 18 comparator_063L
xcomp11 5 13 5 14 15 comparator_063L
xlimit21 5 21 22 limit_063L
xlimit11 5 11 12 limit_063L
egate2 29 5  28 5 1
egate1 101 5  19 5 1
xled2 5 21 3 4 fastled_063L
xled1 5 11 2 1 fastled_063L
cbe2 201 5 10e-12
cce2 6 5 20e-12
c21 23 5 1e-9
c22 26 5 1e-9
c12 17 5 1e-9
c11 14 5 1e-9
cbe1 102 5 10e-12
cce1 7 5 20e-12
rc2 6 203 15
re2 202 5 1
rb2 29 201 1e3
r21 22 23 45
r22 25 26 45
r12 16 17 45
r11 12 14 45
rb1 101 102 1e3
re1 103 5 1
rc1 7 104 15
q2 203 201 202  npnmod
q1 104 102 103  npnmod
.MODEL nmosswitch nmos (vto=+0.7)
.MODEL nmosmod nmos (vto=+0.5   RS=0.3  LAMBDA=0)
.MODEL npnmod  npn bf=2  
.MODEL pnpmod  pnp bf=100 
.MODEL dmod D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07


.subckt icc_063L 1 2 
r2 3 1 157
r1 6 4 100
qnpn2 4 3 1  npnmod
qnpn1 5 4 3  npnmod
qpnp2 5 5 2  pnpmod
qpnp1 6 5 2  pnpmod
.MODEL nmosswitch nmos (vto=+0.7)
.MODEL nmosmod nmos (vto=+0.5   RS=0.3  LAMBDA=0)
.MODEL npnmod  npn bf=2  
.MODEL pnpmod  pnp bf=100 
.MODEL dmod D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07
.ends icc_063L

.subckt limitl_063L 1 2 3
eisolate 3 1  8 1 1
elimit 4 1  2 7 10
vnegativ 6 1 DC -9
vthreshold 7 1 DC 2
vpositive 5 1 DC 10
dnega 6 8 dmod
dposi 8 5 dmod
r2 4 8 1e3
r1 2 7 10e6
.MODEL nmosswitch nmos (vto=+0.7)
.MODEL nmosmod nmos (vto=+0.5   RS=0.3  LAMBDA=0)
.MODEL npnmod  npn bf=2  
.MODEL pnpmod  pnp bf=100 
.MODEL dmod D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07
.ends limitl_063L

.subckt comparator_063L 1 2 3 4 5
mcopa 5 6 1 1 nmosswitch L=1e-6 W=100e-6
egain 6 1  3 4 1e3
ro 5 2 10e3
rgate 6 1 10e3
r1n 3 4 1e6
.MODEL nmosswitch nmos (vto=+0.7)
.MODEL nmosmod nmos (vto=+0.5   RS=0.3  LAMBDA=0)
.MODEL npnmod  npn bf=2  
.MODEL pnpmod  pnp bf=100 
.MODEL dmod D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07
.ends comparator_063L

.subckt limit_063L 1 2 3
eisolate 3 1  8 1 1
elimit 4 1  2 7 10
vnegativ 6 1 DC -9
vthreshold 7 1 DC 0
vpositive 5 1 DC 10
dnega 6 8 dmod
dposi 8 5 dmod
r2 4 8 1e3
r1 2 7 1e6
.MODEL nmosswitch nmos (vto=+0.7)
.MODEL nmosmod nmos (vto=+0.5   RS=0.3  LAMBDA=0)
.MODEL npnmod  npn bf=2  
.MODEL pnpmod  pnp bf=100 
.MODEL dmod D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07
.ends limit_063L

.subckt fastled_063L 1 3 4 5
gband 1 3  9 1 1 
fphoto 1 9  vsense  1 
cdelay 9 1 1e-12
cband 3 1 1e-12
ithre 3 1 DC 3.7e-3
vsense 8 4 DC 0
dled 6 8 lednor
rband 3 1 1e3 
rthermo 9 1 1 
rled 5 6 1
.MODEL  LEDNOR D IS=5E-16 N=2 XTI=3 EG=2.1 BV=5 IBV=10u
+  CJO=6p VJ=.75 M=.3333 FC=.5 TT=20n
.ends fastled_063L
.ends HCPL-063L

* HCPL-4504 SPICE Macromodel 
* Rev. A
* 10/07
* ZFC 
*
* This is the behavioural model for the above-mentioned part number.
* It is valid for functional simulation over the range specified below.
* Temperature = 25C
* Supply range: 0 to 30V
*
* Macromodels provided by Avago Technologies are not warranted
* as fully representing all of the specifications and operating 
* characteristics of the product. 
*
* Macromodels are useful for evaluating product performance but they
* cannot model exact device performance under all conditions, nor are
* they intended to replace breadboarding for final verification.
*
* Copyright 2007 Avago Technologies Limited. All Rights Reserved
********************************************************************************
*This  is      pin2  to  pin8
*                 |       |
.subckt HCPL-4504 2 3 5 6 8 
cbe 20 5 20e-12
cce 6 5 50e-12
rb 20 21 1
re 23 5 1
rc 6 22 15
qout 22 21 23  npnmod
xled 8 20 3 2 led_4504
.MODEL npnmod  npn bf=120 


.subckt led_4504 1 3 4 5
gband 1 3  9 1 3.2e-3 
fphoto 1 9  vsense  1 
egain 7 4  6 4 1
cdelay 9 1 30e-9
cband 3 1 10e-12
vsense 8 4 DC 0
dled 6 4 lednor
doptic 7 8 lednorc
rband 3 1 750e6 
rthermo 9 1 1 
rled 5 6 1
.MODEL  LEDNOR D IS=5E-16 N=2 XTI=3 EG=2.1 BV=5 IBV=10u
+  CJO=6p VJ=.75 M=.3333 FC=.5 TT=20n
.MODEL LEDNORC D IS=5E-16 N=2 XTI=3 EG=2.1 BV=5 IBV=10u
+  VJ=.75 M=.3333 FC=.5 
.ends led_4504
.ends HCPL-4504

* HCPL-4506 SPICE model
*
*                Anode
*                |   Cathode
*                |   |   Common
*                |   |   |   Output
*                |   |   |   |   RL
*                |   |   |   |   |   Vcc
*                |   |   |   |   |   |
.SUBCKT HCPL-4506 102 103 105 106 107 108
land 20  102 20n
lcth 23  103 20n
lgnd 100 105 20n
lout 17  106 20n
lres 117 107 20n
lvdd 1   108 20n
xled 20 23 22 23 led_4506
rled 22 23 1meg
vpd  4 100 2.7
dpd  6 4 dmod1
gpd  4 6 22 23 1.06m
ia1    4    9 100u
ia2    4   10 200u
ra1    6    9  36k
ra2    9   16  3k
ra3   16   10  24k
da1    6    9 dmod2
qa1    9    6  100 npnmod1
qa2   10   16  100 npnmod1 3
qout   17   10  100 npnmod1 3
dqout  100 17 dmod3 1.430
dclp2   10   17 dmod2 2
rl    1  117  20k
.MODEL DMOD1 D IS=2.1E-13 N=1.1 CJO=19.7p M=.4609 VJ=.6302 TT=.1n
.MODEL DMOD2 D N=1.072 TT=.1N CJO=.267P VJ=.69 M=.495 EG=.69
+                IS=8.56E-12 RS=875.8
.MODEL DMOD3 D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07
.MODEL NPNMOD1 NPN IS=2.06E-16 VAF=92.2 NF=.998
+      BF=297 ISE=5.990E-17
+      NE=1.308 IKF=9.568E-3 RE=3.039 RC=36.19 RB=323.1 IRB=30.88U RBM=66.12
+      BR=892.9M ISC=8.844E-16 NR=1.005 VAR=2.872 NC=1.1 IKR=5.201E-3
+      CJE=.23P MJE=.7278 VJE=.9627 CJC=.157P MJC=.3924 VJC=.580
+      CJS=.505P MJS=.1387 VJS=.3757 FC=.5 XTB=1.52 TR=0.2E-7
+      XCJC=.35 TF=.116N PTF=46.26 XTF=213.8 VTF=.568 ITF=113.2E-3


.SUBCKT LED_4506  1 2 7 4
RSERIES 1 5 2.768
DELECT 5 2 LEDNOR
ELED 6 4 5 2 1
DOPTIC 6 8 LEDNORC
VSENSE 8 4 DC 0
FPHOTO 4 3 VSENSE 1
RL 3 4 1 
EOUT 7 4 3 4 1
.MODEL LEDNOR D IS=330E-21 N=1.5 XTI=3 EG=1.52 BV=10.38 IBV=100u
+  CJO=60p VJ=.75 M=.3333 FC=.5 TT=20n
.MODEL LEDNORC D IS=330E-21 N=1.5 XTI=3 EG=1.52 BV=10.38 IBV=100u
+  VJ=.75 M=.3333 FC=.5 
.ENDS LED_4506
.ENDS HCPL-4506

* HCPL-3140/HCPL-0314 SPICE Macromodel 
* Rev. B
* 07/07
* ZFC 
*
* This is the behavioural model for the above-mentioned part number.
* It is valid for functional simulation over the range specified below.
* Supply range: 10 to 30V
*
* Macromodels provided by Avago Technologies are not warranted
* as fully representing all of the specifications and operating 
* characteristics of the product. 
*
* Macromodels are useful for evaluating product performance but they
* cannot model exact device performance under all conditions, nor are
* they intended to replace breadboarding for final verification.
*
* Copyright 2007 Avago Technologies Limited. All Rights Reserved
********************************************************************************
*                 Anode(pin2)
*                  |    Cathode(pin3)
*                  |    |    Common(pin5)
*                  |    |    |    Output(pin6)  
*                  |    |    |    |    Vcc(pin8)
*                  |    |    |    |    |
.SUBCKT HCPL-3140  102  103  105  106  108
land 20  102 20n
lcth 23  103 20n
lgnd 10  105 20n
lout 3   106 20n
lvcc 2   108 20n
xled 20 23 22 23 led_3140
rled 22 23 1meg
*This is Ith, bandwidth and amplitude limitation
gpd  10 1 22 23 1
ith 1  10  dc 4.3m
cb  1  10 0.16n 
rb  1  10 1500 
vlimitp  13 10 dc 1
vlimitn  14 10 dc -1
dlimitp  11 13 dmod3 1.430 
dlimitn  14 11 dmod3 1.430
rlimit 12 11 1000
elimit 12 10 1 10 1
mshut 11 16 10 10 nmosmod l=10u  w=1000000u
.MODEL nmosmod nmos (vto=+0.7)
vshut 15 10 dc 5
rshut1 15 16 1000
rshut2 16 17 1000
eshut 17 10  2 10 -1
*This is output stage and gate drive 
vinside 28 10 dc 30
ql1  3  6  10 npnmodL
ql2  28  7  6  npnmodL
dl1  10  3  dmod3 1.430
dl2  10  6  dmod3 1.430
dl3  6   7  dmod3 1.430
qh1  2  4  3 npnmodH
qh2  2  5  4 npnmodH
dh1  3  2 dmod3 1.430
dh2  3  4 dmod3 1.430
dh3  4  5 dmod3 1.430
gidh   3   5  11 10  0.0005 
gidl  10   7  10 11  0.0005
*This is iccl 1.2mA current source cricuit
q3 51 50 2 pnpmod
q4 50 50 2 pnpmod
q2 50 52 53 npnmod
q1 52 53 58 npnmod
rst2 51 52 1000
riccl 53 58 1240 
qcl 58 59 60 npnmod
gcl 60 59 10 11 0.0001
qdl 60 61 10 npnmod
gdl 10 61 2 10 0.000003
d50 60 59 dmod3 1.430
d51 10 61 dmod3 1.430
rl 58 10 1000000
.MODEL pnpmod  pnp (bf=100)
.MODEL npnmod  npn (bf=100)
*This is icch 0.7mA current source cricuit
q13 151 150 2 pnpmod
q14 150 150 2 pnpmod
q12 150 152 153 npnmod
q11 152 153 158 npnmod
rst12 151 152 1000
ricch 153 158 2000 
qch 158 159 160 npnmod
gch 160 159 11 10 0.0001
qdh 160 161 10 npnmod
gdh 10 161 2 10 0.000002
d150 160 159 dmod3 1.430
d151 10 161 dmod3 1.430
rh 158 10 1000000
.MODEL DMOD3 D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07
.MODEL NPNMODH
+ NPN 
+ IS=1.27E-17 BF=550.35 NF=1.00 ISE=4.61E-17 NE=1.320 
+ IKF=7.06E-3 BR=0.11  
+ NR=1.00 IKR=10.4E-3 VAR=17.47 VAF=18 RE=4.26 RC=4.12 RB=6.85
+ RBM=68.65
+ CJC=84E-15 CJE=38E-15 CJS=520E-15 MJE=0.50 VJE=0.69 MJC=0.40 VJC=0.60
+ MJS=0.344 VJS=0.60 TF=305P XTF=0.43   ITF=12M
+ IRB=95U PTF=17  XTB=0.202 XTI=3.058 TR=85E-9 EG=1.167
.MODEL NPNMODL
+ NPN
 + IS=1.27E-17 BF=600.35 NF=1.00 ISE=4.61E-17 NE=1.320 
+ IKF=7.06E-3 BR=0.11  
+ NR=1.00 IKR=10.4E-3 VAR=17.47 VAF=32 RE=.136 RC=2.5 RB=6.85
+ RBM=68.65
+ CJC=84E-15 CJE=38E-15 CJS=520E-15 MJE=0.50 VJE=0.69 MJC=0.40 VJC=0.60
+ MJS=0.344 VJS=0.60 TF=305P XTF=0.43   ITF=12M
+ IRB=95U PTF=17  XTB=0.202 XTI=3.058 TR=85E-9 EG=1.167

.SUBCKT led_3140  1 2 7 4
RSERIES 1 5 2
DELECT 5 2 LEDNOR
ELED 6 4 5 2 1
DOPTIC 6 8 LEDNORC
VSENSE 8 4 DC 0
FPHOTO 4 3 VSENSE 1
RL 3 4 1 
EOUT 7 4 3 4 1
.MODEL LEDNOR D IS=5E-16 N=2 XTI=3 EG=2.1 BV=5 IBV=10u
+  CJO=60p VJ=.75 M=.3333 FC=.5 TT=20n
.MODEL LEDNORC D IS=5E-16 N=2 XTI=3 EG=2.1 BV=5 IBV=10u
+  VJ=.75 M=.3333 FC=.5 
.ENDS led_3140
.ENDS HCPL-3140

* HCPL-3120 SPICE Macromodel 
* (also applies to HCPL-J312)
* Rev. A
* 09/07
* ZFC 
*
* This is the behavioural model for the above-mentioned part number.
* It is valid for functional simulation over the range specified below.
* (over recommended operating conditions as specified in the datasheet)
*
* Macromodels provided by Avago Technologies are not warranted
* as fully representing all of the specifications and operating 
* characteristics of the product. 
*
* Macromodels are useful for evaluating product performance but they
* cannot model exact device performance under all conditions, nor are
* they intended to replace breadboarding for final verification.
*
* Copyright 2007 Avago Technologies Limited. All Rights Reserved
********************************************************************************
*
*               pin1     to    pin8
*                 |             |
.subckt HCPL-3120 1 2 3 4 5 6 7 8 
q2icc 163 84 8  pnpmod
q1icc 84 84 8  pnpmod
xled 5 10 3 2 led_3120
xlimitl 5 10 50 limitl_3120
ehdrive 42 7  40 5 1
eldrive 51 5  50 5 -1
xhlimit 5 10 40 limit_3120
vuvlo 20 5 DC 12
vccinside 22 5 DC 30
dmosl 5 7 dmod
ddrive3 47 46 dmod
ddrive2 49 47 dmod
ddrive1 7 49 dmod
q4icc 122 124 5  npnmod
q3icc 84 122 124  npnmod
qdrive3 8 46 47  NPNMOD
qdrive2 8 47 49  NPNMOD
qdrive1 8 49 48  NPNMOD
xuvlo 5 22 8 21 23 comparator_3120
muvlo 10 24 5 5 nmosswitch L=1e-6 W=100e-6
mdrive 7 52 5 5 nmosmod L=1e-6 W=10e-3
cuvlo 24 5 20e-12
chigh 8 7 1e-12
clgate 52 5 1e-12
clow 7 5 1e-12
chgate 46 7 300e-12 
rshort67  6 7  0.0001
ruvlo2 23 21 40e3
r2icc 124 5 600
ruvlo3 23 24 1e3
r1icc 163 122 1e3
rhgate 42 46 1e3
rlgate 51 52 1e3
ruvlo 21 20 1e3
rehdrive 48 7 1.4 

.MODEL nmosswitch nmos (vto=+0.7)
.MODEL nmosmod nmos (vto=+0.5   RS=0.9  LAMBDA=0)
.MODEL npnmod  npn bf=16
.MODEL pnpmod  pnp bf=100 
.MODEL dmod D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07


.subckt led_3120 1 3 4 5
gband 1 3  9 1 1 
fphoto 1 9  vsense  1 
egain 7 4  6 4 1
cband 3 1 140e-12
ithre 3 1 DC 500e-6
vsense 8 4 DC 0
dled 6 4 lednor
doptic 7 8 lednorc
rband 3 1 1e3 
rthermo 9 1 1 
rled 5 6 1
.MODEL LEDNOR D IS=5E-16 N=2 XTI=3 EG=2.1 BV=5 IBV=10u
+  CJO=60p VJ=.75 M=.3333 FC=.5 TT=20n
.MODEL LEDNORC D IS=5E-16 N=2 XTI=3 EG=2.1 BV=5 IBV=10u
+  VJ=.75 M=.3333 FC=.5 
.ends led_3120

.subckt limitl_3120 1 2 3
elimit 4 1  2 7 10
vnegativ 6 1 DC -12
vthreshold 7 1 DC 2
vpositive 5 1 DC  12
dnega 6 3 dmod
dposi 3 5 dmod
r2 4 3 1e3
r1 2 7 10e6
.MODEL nmosswitch nmos (vto=+0.7)
.MODEL nmosmod nmos (vto=+0.5   RS=0.9  LAMBDA=0)
.MODEL npnmod  npn bf=16
.MODEL pnpmod  pnp bf=100 
.MODEL dmod D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07
.ends limitl_3120

.subckt limit_3120 1 2 3
elimit 4 1  2 7 10
vnegativ 6 1 DC -10
vthreshold 7 1 DC 2
vpositive 5 1 DC 10
dnega 6 3 dmod
dposi 3 5 dmod
r2 4 3 1e3
r1 2 7 1e6
.MODEL nmosswitch nmos (vto=+0.7)
.MODEL nmosmod nmos (vto=+0.5   RS=0.9  LAMBDA=0)
.MODEL npnmod  npn bf=16
.MODEL pnpmod  pnp bf=100 
.MODEL dmod D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07
.ends limit_3120

.subckt comparator_3120 1 2 3 4 5
mcopa 5 6 1 1 nmosswitch L=1e-6 W=100e-6
egain 6 1  3 4 1e3
ro 5 2 10e3
rgate 6 1 10e3
r1n 3 4 1e6
.MODEL nmosswitch nmos (vto=+0.7)
.MODEL nmosmod nmos (vto=+0.5   RS=0.9  LAMBDA=0)
.MODEL npnmod  npn bf=16
.MODEL pnpmod  pnp bf=100 
.MODEL dmod D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07
.ends comparator_3120
.ends HCPL-3120

* HCPL-0302 SPICE Macro-model 
* (also applies to HCPL-3020)
* Rev. A
* 08/07
* ZFC 
*
* This is the behavioural model for the above-mentioned part number.
* It is valid for functional simulation over the range specified below.
* (over recommended operating conditions as specified in the datasheet)  
*
* Macromodels provided by Avago Technologies are not warranted
* as fully representing all of the specifications and operating 
* characteristics of the product. 
*
* Macromodels are useful for evaluating product performance but they
* cannot model exact device performance under all conditions, nor are
* they intended to replace breadboarding for final verification.
*
* Copyright 2007 Avago Technologies Limited. All Rights Reserved
********************************************************************************
* 
*                  Anode(pin2)
*                  |    Cathode(pin3)
*                  |    |    Common(pin5)
*                  |    |    |    Output(pin6)  
*                  |    |    |    |    Vcc(pin8)
*                  |    |    |    |    |
.SUBCKT HCPL-0302  102  103  105  106  108
land 20  102 20n
lcth 23  103 20n
lgnd 10  105 20n
lout 3   106 20n
lvcc 2   108 20n
xled 20 23 22 23 led_0302
rled 22 23 1meg
*This is Ith,bandwith and amplitute limitation
gpd  10 1 22 23 1
ith 1  10  dc 4.3m
cb  1  10 0.13n 
rb  1  10 1500 
vlimitp  13 10 dc 1
vlimitn  14 10 dc -1
dlimitp  11 13 dmod3 1.430 
dlimitn  14 11 dmod3 1.430
rlimit 12 11 1000
elimit 12 10 1 10 1
mshut 11 16 10 10 nmosmod l=10u  w=1000000u
.MODEL nmosmod nmos (vto=+0.7)
vshut 15 10 dc 5
rshut1 15 16 1000
rshut2 16 17 1000
eshut 17 10  2 10 -1
*This is output stage and gate drive 
vinside 28 10 dc 30
ql1  3  6  10 npnmodL
ql2  28  7  6  npnmodL
dl1  10  3  dmod3 1.430
dl2  10  6  dmod3 1.430
dl3  6   7  dmod3 1.430
qh1  2  4  3 npnmodH
qh2  2  5  4 npnmodH
dh1  3  2 dmod3 1.430
dh2  3  4 dmod3 1.430
dh3  4  5 dmod3 1.430
gidh   3   5  11 10  0.0005 
gidl  10   7  10 11  0.0005
*This is iccl 1.2mA current source cricuit
q3 51 50 2 pnpmod
q4 50 50 2 pnpmod
q2 50 52 53 npnmod
q1 52 53 58 npnmod
rst2 51 52 1000
riccl 53 58 1240 
qcl 58 59 60 npnmod
gcl 60 59 10 11 0.0001
qdl 60 61 10 npnmod
gdl 10 61 2 10 0.000003
d50 60 59 dmod3 1.430
d51 10 61 dmod3 1.430
rl 58 10 1000000
.MODEL pnpmod  pnp (bf=100)
.MODEL npnmod  npn (bf=100)
*This is icch 0.7mA current source cricuit
q13 151 150 2 pnpmod
q14 150 150 2 pnpmod
q12 150 152 153 npnmod
q11 152 153 158 npnmod
rst12 151 152 1000
ricch 153 158 2000 
qch 158 159 160 npnmod
gch 160 159 11 10 0.0001
qdh 160 161 10 npnmod
gdh 10 161 2 10 0.000002
d150 160 159 dmod3 1.430
d151 10 161 dmod3 1.430
rh 158 10 1000000
.MODEL DMOD3 D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07
.MODEL NPNMODH
+ NPN 
+ IS=1.27E-17 BF=550.35 NF=1.00 ISE=4.61E-17 NE=1.320 
+ IKF=7.06E-3 BR=0.11  
+ NR=1.00 IKR=10.4E-3 VAR=17.47 VAF=18 RE=4.26 RC=4.12 RB=6.85
+ RBM=68.65
+ CJC=84E-15 CJE=38E-15 CJS=520E-15 MJE=0.50 VJE=0.69 MJC=0.40 VJC=0.60
+ MJS=0.344 VJS=0.60 TF=305P XTF=0.43   ITF=12M
+ IRB=95U PTF=17  XTB=0.202 XTI=3.058 TR=85E-9 EG=1.167
.MODEL NPNMODL
+ NPN
 + IS=1.27E-17 BF=600.35 NF=1.00 ISE=4.61E-17 NE=1.320 
+ IKF=7.06E-3 BR=0.11  
+ NR=1.00 IKR=10.4E-3 VAR=17.47 VAF=32 RE=.136 RC=2.5 RB=6.85
+ RBM=68.65
+ CJC=84E-15 CJE=38E-15 CJS=520E-15 MJE=0.50 VJE=0.69 MJC=0.40 VJC=0.60
+ MJS=0.344 VJS=0.60 TF=305P XTF=0.43   ITF=12M
+ IRB=95U PTF=17  XTB=0.202 XTI=3.058 TR=85E-9 EG=1.167

.SUBCKT LED_0302  1 2 7 4
RSERIES 1 5 2
DELECT 5 2 LEDNOR
ELED 6 4 5 2 1
DOPTIC 6 8 LEDNORC
VSENSE 8 4 DC 0
FPHOTO 4 3 VSENSE 1
RL 3 4 1 
EOUT 7 4 3 4 1
.MODEL LEDNOR D IS=5E-16 N=2 XTI=3 EG=2.1 BV=5 IBV=10u
+  CJO=60p VJ=.75 M=.3333 FC=.5 TT=20n
.MODEL LEDNORC D IS=5E-16 N=2 XTI=3 EG=2.1 BV=5 IBV=10u
+  VJ=.75 M=.3333 FC=.5 
.ENDS LED_0302
.ENDS HCPL0302

* HCPL-181 SPICE Macro-model 
* Rev. A
* 09/07
* ZFC 
*
* This is the behavioural model for the above-mentioned part number.
* It is valid for functional simulation over the range specified below.
* Temperature: 25C
*
* Macromodels provided by Avago Technologies are not warranted
* as fully representing all of the specifications and operating 
* characteristics of the product. 
*
* Macromodels are useful for evaluating product performance but they
* cannot model exact device performance under all conditions, nor are
* they intended to replace breadboarding for final verification.
*
* Copyright 2007 Avago Technologies Limited. All Rights Reserved
********************************************************************************

*              pin1 to pin4
*                |     |
.subckt HCPL-181 1 2 3 4 
cbe 5 3 100e-12
cce 4 3 100e-12
ccb 5 4 100e-12
re 7 3 1
rc 4 6 50
qout 6 5 7  NPNMOD
xled 4 5 2 1 led_181
.MODEL npnmod  npn bf=2

.subckt led_181 1 3 4 5
gband 1 3  9 1 1 
fphoto 1 9  vsense  1 
egain 7 4  6 4 1
cdelay 9 1 1.5e-6
cband 3 1 14e-12
ithre 3 1 DC  1e-6
vsense 8 4 DC  0
dled 6 4 lednor
doptic 7 8 lednorc
rband 3 1 100e3 
rthermo 9 1 1 
rled 5 6 1
.MODEL LEDNOR D IS=5E-16 N=1.5 XTI=3 EG=2.1 BV=5 IBV=10u
+  CJO=60p VJ=.75 M=.3333 FC=.5 TT=20n
.MODEL LEDNORC D IS=5E-16 N=1.5 XTI=3 EG=2.1 BV=5 IBV=10u
+  VJ=.75 M=.3333 FC=.5 
.ends led_181
.ends HCPL-181

* 6N136 SPICE Macromodel 
* Rev. A
* 08/07
* ZFC 
*
* This is the behavioural model for the above-mentioned part number.
* It is valid for functional simulation over the range specified below.
* (over recommended operating conditions as specified in the datasheet)  
*
* Macromodels provided by Avago Technologies are not warranted
* as fully representing all of the specifications and operating 
* characteristics of the product. 
*
* Macromodels are useful for evaluating product performance but they
* cannot model exact device performance under all conditions, nor are
* they intended to replace breadboarding for final verification.
*
* Copyright 2007 Avago Technologies Limited. All Rights Reserved
********************************************************************************
* 6N136 SPICE model
*                  Anode(pin2)
*                  |    Cathode(pin3)
*                  |    |    Common(pin5)
*                  |    |    |    Output(pin6)
*                  |    |    |    |    VB(pin7)
*                  |    |    |    |    |    Vcc(pin8)
*                  |    |    |    |    |    |
.SUBCKT HCPL6N136 102  103  105  106  107  108
land 20  102 20n
lcth 23  103 20n
lgnd 100 105 20n
lout 17  106 20n
lbas 10  107 20n
lvdd 4   108 20n
xled 20 23 22 23 led_136
rled 22 23 1meg
gpd  4 10 22 23 2.5m
dpd  10 4 dmod1
qout   17   10  100 npnmod1 3
dqout  100 17 dmod3 1.430
.MODEL DMOD1 D IS=2.1E-13 N=1.1 CJO=19.7p M=.4609 VJ=.6302 TT=.1n
.MODEL DMOD3 D IS=2.22P CJO=1P VJ=.376 M=.139 N=1.07
.MODEL NPNMOD1 NPN IS=2.06E-16 VAF=17.2 NF=.998
+      BF=185 ISE=5.990E-17
+      NE=1.308 IKF=9.568E-3 RE=2.039 RC=5 RB=323.1 IRB=30.88U RBM=66.12
+      BR=892.9M ISC=8.844E-16 NR=1.005 VAR=2.872 NC=1.1 IKR=5.201E-3
+      CJE=.23P MJE=.7278 VJE=.9627 CJC=.157P MJC=.3924 VJC=.580
+      CJS=.505P MJS=.1387 VJS=.3757 FC=.5 XTB=0.6 TR=12.0E-7
+      XCJC=.35 TF=2.0N PTF=46.26 XTF=213.8 VTF=.568 ITF=113.2E-3


.SUBCKT LED_136   1 2 7 4
RSERIES 1 5 1
DELECT 5 2 LEDNOR
ELED 6 4 5 2 1
DOPTIC 6 8 LEDNORC
VSENSE 8 4 DC 0
FPHOTO 4 3 VSENSE 1
RL 3 4 1 
EOUT 7 4 3 4 1
.MODEL LEDNOR D IS=5E-16 N=2 XTI=3 EG=2.1 BV=5 IBV=10u
+  CJO=60p VJ=.75 M=.3333 FC=.5 TT=20n
.MODEL LEDNORC D IS=5E-16 N=2 XTI=3 EG=2.1 BV=5 IBV=10u
+  VJ=.75 M=.3333 FC=.5 
.ENDS LED_136
.ENDS HCPL6N136 